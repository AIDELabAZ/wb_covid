* Project: WB COVID
* Created on: July 2020
* Created by: alj
* Edited by: jdm, amf, lirr (style edits)
* Last edited: 11 July 2022
* Stata v.17.0

* does
	* merges together each section of malawi data
	* builds round 3
	* outputs round 3

* assumes
	* raw malawi data 

* TO DO:
	* complete


************************************************************************
**# - setup
************************************************************************

* define
	global	root	=	"$data/malawi/raw"
	global	export	=	"$data/malawi/refined"
	global	logout	=	"$data/malawi/logs"
	global  fies 	= 	"$data/analysis/raw/Malawi"

* open log
	cap log 		close
	log using		"$logout/mal_build", append
	
* set local wave number & file number
	local			w = 3
	
* make wave folder within refined folder if it does not already exist 
	capture mkdir "$export/wave_0`w'" 	
	
*************************************************************************
**# - reshape section on income loss wide data
*************************************************************************

* load income_loss data
	use				"$root/wave_0`w'/sect7_Income_Loss_r`w'", clear
		*** obs == 19488

* drop other source
	drop 			income_source_os
		*** obs == 19488
		
*reshape data
	reshape 		wide s7q1 s7q2, i(y4_hhid HHID) j(income_source)
		*** obs == 1624
	
* save temp file
	tempfile		tempa
	save			`tempa'

	
*************************************************************************
**# - reshape section on safety nets wide data
*************************************************************************

* load safety_net data - updated via convo with Talip 9/1
	use				"$root/wave_0`w'/sect11_Safety_Nets_r`w'", clear
		*** obs == 8120

* drop other
	drop 			s11q2 s11q3 s11q3_os s11q4a s11q4b s11q5 s11q6__1 ///
						s11q6__2 s11q6__3 s11q6__4 s11q6__5 s11q6__6 ///
						s11q6__7 s11q7__1 s11q7__2 s11q7__3 s11q7__4 ///
						s11q7__5 s11q7__6 s11q7__7 s11q7_os version
							*** obs == 8120
							
* reshape
	reshape 		wide s11q1, i(y4_hhid HHID) j(social_safety)
		*** obs == 1624
 
* collapse cash options into 1 to match round 1 
	rename 			s11q12 temp
	gen 			s11q12 = 1 if temp == 1 | s11q13 == 1 | s11q14 == 1
		*** obs == 1624
	replace 		s11q12 = 2 if s11q12 == .
	drop 			s11q13 s11q14  temp
		*** obs == 1624
	rename 			s11q15 s11q13

* save temp file
	tempfile		tempb
	save			`tempb'
	

*************************************************************************
**# - get respondant gender
*************************************************************************

* load data
	use				"$root/wave_0`w'/sect12_Interview_Result_r`w'", clear
		*** obs == 1624

* drop all but household respondant
	keep			HHID s12q9
		*** obs == 1624
	rename			s12q9 PID
	isid			HHID

* merge in household roster
	merge 1:1		HHID PID using "$root/wave_0`w'/sect2_Household_Roster_r`w'.dta"
		*** obs == 8405: 1624 matched, 6781 unmatched
	keep if			_merge == 3
		*** obs == 1624
	drop			_merge
		*** obs == 1624
		
* drop all but gender and relation to HoH
	keep			HHID PID s2q5 s2q6 s2q7 s2q9
		*** obs == 1624

* save temp file
	tempfile		tempc
	save			`tempc'
	
	
*************************************************************************
**# - get household size and gender of HOH
*************************************************************************

* load data
	use			"$root/wave_0`w'/sect2_Household_Roster_r`w'.dta", clear
		*** obs == 8405

* rename other variables 
	rename 			PID ind_id 
	rename 			s2q3 curr_mem
	replace 		curr_mem = 1 if s2q2 == 1
	rename 			s2q5 sex_mem
	rename 			s2q6 age_mem
	rename 			s2q7 relat_mem	
	replace			relat_mem = s2q9 if relat_mem == . 
	
* generate counting variables
	gen				hhsize = 1 if curr_mem == 1
	gen 			hhsize_adult = 1 if curr_mem == 1 & age_mem > 18 & age_mem < .
	gen				hhsize_child = 1 if curr_mem == 1 & age_mem < 19 & age_mem != . 
	gen 			hhsize_schchild = 1 if curr_mem == 1 & age_mem > 4 & age_mem < 19 
		*** obs == 8405
		
* create hh head gender
	gen 			sexhh = . 
	replace			sexhh = sex_mem if relat_mem == 1
	label var 		sexhh "Sex of household head"

* generate migration vars
	rename 			new_member new_mem
	replace 		new_mem = 0 if s2q8 == 10
	replace 		s2q8 = . if s2q8 == 10
	gen 			mem_left = 1 if curr_mem == 2
	replace 		new_mem = 0 if new_mem == 2
	replace 		mem_left = 0 if mem_left == 2
	
	* why member left
		preserve
			keep 		y4 s2q4 ind_id
				*** obs == 8405
			keep 		if s2q4 != .
				*** obs == 160
			duplicates 	drop y4 s2q4, force
				*** obs == 136
			reshape 	wide ind_id, i(y4) j(s2q4)
				*** obs == 123
			ds 			ind_id*
			foreach 	var in `r(varlist)' {
				replace 	`var' = 1 if `var' != .
			}
			rename 		ind_id* mem_left_why_*
			tempfile 	mem_left
			save 		`mem_left'
		restore
	
	* why new member 
		preserve
			keep 		y4 s2q8 ind_id
				*** obs == 8405
			keep 		if s2q8 != .
				*** obs == 106
			duplicates 	drop y4 s2q8, force
				*** obs == 95
			reshape 	wide ind_id, i(y4) j(s2q8)
				*** obs == 90
			ds 			ind_id*
			foreach 	var in `r(varlist)' {
				replace 	`var' = 1 if `var' != .
			}
			rename 		ind_id* new_mem_why_*
			tempfile 	new_mem
			save 		`new_mem'
		restore
	
* collapse data to hh level and merge in why vars
	collapse	(sum) hhsize hhsize_adult hhsize_child hhsize_schchild new_mem mem_left ///
				(max) sexhh, by(HHID y4)
					*** obs == 1624
	replace 	new_mem = 1 if new_mem > 0 & new_mem < .
	replace 	mem_left = 1 if mem_left > 0 & new_mem < .			
	merge 		1:1 y4 using `new_mem', nogen
		*** obs == 1624: 90 matched, 1534 unmatched
	merge 		1:1 y4 using `mem_left', nogen
		*** obs == 1624: 123 matched, 1501 unmatched
	ds 			new_mem_why_* 
	foreach		var in `r(varlist)' {
		replace 	`var' = 0 if `var' >= . & new_mem == 1
	}
	ds 			mem_left_why_* 
	foreach		var in `r(varlist)' {
		replace 	`var' = 0 if `var' >= . & mem_left == 1
	}
	lab var		hhsize "Household size"
	lab var 	hhsize_adult "Household size - only adults"
	lab var 	hhsize_child "Household size - children 0 - 18"
	lab var 	hhsize_schchild "Household size - school-age children 5 - 18"
	lab var 	mem_left "Member of household left since last call"
	lab var 	new_mem "Member of household joined since last call"
	drop 		y4
		*** obs == 1624

* save temp file
	tempfile		tempd
	save			`tempd'
	
	
*************************************************************************
**# - FIES score
*************************************************************************

* load data
	use				"$fies/MW_FIES_round`w'.dta", clear
		*** obs == 1624
	drop 			country round
		*** obs == 1624

* merge in other data to get HHID to match 
	rename 			HHID y4_hhid 
	merge 			1:1 y4_hhid using "$root/wave_0`w'/secta_Cover_Page_r`w'"
		*** obs == 1720: 1624 matched, 96 unmatched
	keep 			HHID hhsize wt_hh p_mod urban weight Above_18 wt_18 p_sev
		*** obs == 1720

* save temp file
	tempfile		tempe
	save			`tempe'


*************************************************************************
**# - reshape section on coping wide data
*************************************************************************

* load data
	use				"$root/wave_0`w'/sect10_Coping_r`w'", clear
		*** obs == 14616

* drop other shock
	drop			shock_id_os s10q3_os
		*** obs == 14616

* generate shock variables
	foreach 		i in 5 6 7 8 10 11 12 13 95 {
	    gen				shock_`i' = 0 if s10q1 == 2
		replace			shock_`i' = 1 if s10q1 == 1 & shock_id == `i'
	}

* collapse to household level	
	collapse (max) s10q2__1- shock_95, by(HHID y4_hhid)
		*** obs == 1624
	
* save temp file
	tempfile		tempf
	save			`tempf'
	
	
*************************************************************************
**# - merge to build complete dataset for the round 
*************************************************************************

* load cover data
	use				"$root/wave_0`w'/secta_Cover_Page_r`w'", clear
		*** obs == 1720
	
* merge formatted sections
	foreach 		x in a b c d e f {
	    merge 		1:1 HHID using `temp`x'', nogen
	}
		*** obs == 1720: 1624 matched, 96 unmatched for temps abcdf; 1720 matched tempe
		
* merge in other sections
	merge 1:1 		HHID using "$root/wave_0`w'/sect4_Behavior_r`w'.dta", nogen
		*** obs == 1720: 1624 matched, 96 unmatched
	merge 1:1 		HHID using "$root/wave_0`w'/sect5_Access_r`w'.dta", nogen
		*** obs == 1720: 1624 matched, 96 unmatched
	merge 1:1 		HHID using "$root/wave_0`w'/sect6a_Employment1_r`w'.dta", nogen
		*** obs == 1720: 1624 matched, 96 unmatched
	merge 1:1 		HHID using "$root/wave_0`w'/sect6a_Employment2_r`w'.dta", nogen
		*** obs == 1720: 1624 matched, 96 unmatched
	merge 1:1 		HHID using "$root/wave_0`w'/sect6b_NFE_r`w'.dta", nogen
		*** obs == 1720: 1624 matched, 96 unmatched
	merge 1:1 		HHID using "$root/wave_0`w'/sect6c_OtherIncome_r`w'.dta", nogen
		*** obs == 1720: 1624 matched, 96 unmatched
	merge 1:1 		HHID using "$root/wave_0`w'/sect6d_Credit_r`w'.dta", nogen
		*** obs == 1720: 1624 matched, 96 unmatched
	merge 1:1 		HHID using "$root/wave_0`w'/sect8_food_security_r`w'.dta", nogen
		*** obs == 1720: 1624 matched, 96 unmatched
	merge 1:1 		HHID using "$root/wave_0`w'/sect9_Concerns_r`w'.dta", nogen
		*** obs == 1720: 1624 matched, 96 unmatched
	
* rename variables inconsistent with  wave 1
	rename 	 		s6q2_1 emp_pre
	rename			s6q3a_1 emp_pre_why
	rename			s6q3a_1a emp_search
	rename			s6q3a_2a emp_search_how
	rename			s6q4a_1 emp_same
	rename			s6q4b_1 emp_chg_why
	rename			s6q4c_1 emp_pre_act
	rename			s6q6_1 emp_stat
	rename			s6q7_1 emp_able
	rename			s6q8_1 emp_unable
	rename			s6q8a_1 emp_unable_why
	rename			s6q8b_1__1 emp_cont_1
	rename			s6q8b_1__2 emp_cont_2
	rename			s6q8b_1__3 emp_cont_3
	rename			s6q8b_1__4 emp_cont_4

	rename			s6q8c_1 contrct
	replace 		contrct = s6q8e if contrct == .
	drop 			s6q8e
	replace 		contrct = 0 if contrct == 2
	
	rename			s6bq11 bus_emp
	rename			s6q9_1 emp_hh
	rename			s6q15_1 farm_emp
	rename			s6q16_1 farm_norm
	rename			s6q17_1__1 farm_why_1
	rename			s6q17_1__2 farm_why_2
	rename			s6q17_1__3 farm_why_3
	rename			s6q17_1__4 farm_why_4
	rename			s6q17_1__5 farm_why_5
	rename			s6q17_1__6 farm_why_6
	rename			s6q17_1__7 farm_why_8
	drop 			s6q17_1__96
* edit employment activity
	rename			s6q5_1 emp_act
	replace 		emp_act = 11 if emp_act == 4
	replace 		emp_act = 13 if emp_act == 8
	replace 		emp_act = 7 if emp_act == 10
 
	replace 		s6q5 = -96 if s6q5 == 96
	replace 		s6q5 = 14 if s6q5 == 9
	replace 		s6q5 = 9 if s6q5 == 11
	replace 		s6q5 = 11 if s6q5 == 4
	replace 		s6q5 = 4 if s6q5 == 7
	replace 		s6q5 = 7 if s6q5 == 10
	replace 		s6q5 = 13 if s6q5 == 8
	replace 		s6q5 = 8 if s6q5 == 6
	
	lab def 		emp_act -96 "Other" 1 "Agriculture" 2 "Industry/manufacturing" ///
						3 "Wholesale/retail" 4 "Transportation services" ///
						5 "Restaurants/hotels" 6 "Public Administration" ///
						7 "Personal Services" 8 "Construction" 9 "Education/Health" ///
						10 "Mining" 11 "Professional/scientific/technical activities" ///
						12 "Electic/water/gas/waste" 13 "Buying/selling" ///
						14 "Finance/insurance/real estate" 15 "Tourism" 16 "Food processing" 
	lab val 		emp_act emp_act
 
* behavior
	rename			s4q1 bh_1
	rename			s4q2a bh_2
	replace 		bh_2 = 0 if bh_2 < 3
	replace 		bh_2 = 1 if bh_2 > 0 & bh_2 != .
	rename			s4q3a bh_3a
	rename			s4q3b bh_3b
	rename			s4q3c bh_3c
	rename			s4q6 bh_5
	replace 		bh_5 = 0 if bh_5 == 1
	replace 		bh_5 = 1 if bh_5 == 2 | bh_5 == 3
	rename			s4q8 bh_freq_mask
	rename 			s4q9 bh_comply_1
	rename 			s4q10 bh_comply_2
	rename 			s4q11 bh_comply_3
	rename 			s4q12 bh_comply_4
	
* rename access credit variables inconsistent with wave 4	
	rename 			s6dq1 ac_cr_need
	rename 			s6dq2 ac_cr_att
	rename 			s6dq3 ac_cr_loan
	forval 			x = 1/8 {
	    rename 		s6dq4__`x' ac_cr_lend_`x'
	}
	forval 			x = 1/12 {
		gen 			ac_cr_why_`x' = 0
		replace 		ac_cr_why_`x' =	. if s6dq5 == .
		replace 		ac_cr_why_`x' = 1 if s6dq5 == `x'
	}
	drop 			s6dq5
	rename 			s6dq6__0 ac_cr_who_1
	rename 			s6dq6__1 ac_cr_who_2
	rename 			s6dq7 ac_cr_due
	rename 			s6dq8 ac_cr_worry
	rename 			s6dq9 ac_cr_bef	
	forval 			x = 1/12 {
		gen 			ac_cr_bef_why_`x' = 0
		replace 		ac_cr_bef_why_`x' =	. if s6dq10 == .
		replace 		ac_cr_bef_why_`x' = 1 if s6dq10 == `x'
	}
	drop 			s6dq10
		*** obs == 1720
	rename 			s6dq11 ac_cr_bef_worry
	rename 			s6dq12 ac_cr_bef_miss
	rename 			s6dq13 ac_cr_bef_delay	
	
* generate round variables
	gen				wave = `w'
		*** obs == 1720
	lab var			wave "Wave number"
	rename			wt_round`w' phw_cs
	label var		phw "sampling weights - cross section"
	
* save round file
	save			"$export/wave_0`w'/r`w'", replace

/* END */		